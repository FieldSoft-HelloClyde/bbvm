options{
//	DEBUG_PARSER=false;
//  DEBUG_LOOKAHEAD=false;
//  DEBUG_TOKEN_MANAGER=false;
//  FORCE_LA_CHECK=true;

    UNICODE_INPUT=true;
    IGNORE_CASE=true ;
//    MULTI=true;

    VISITOR=true;
    STATIC = false;
    NODE_DEFAULT_VOID=true;
    NODE_EXTENDS="BaseNode";
    NODE_PREFIX="AST";
    NODE_USES_PARSER=true;
    NODE_SCOPE_HOOK=true;
    VISITOR_DATA_TYPE="Object";
    VISITOR_RETURN_TYPE="Object";
    VISITOR_EXCEPTION="";
    TRACK_TOKENS=true;
}

PARSER_BEGIN(BBAsmParser)
package me.wener.bbvm.asm;
import me.wener.bbvm.vm.*;
import static me.wener.bbvm.vm.AddressingMode.*;
import java.util.*;
import java.math.*;
public class BBAsmParser extends BaseBBAsmParser {

}

PARSER_END(BBAsmParser)

SKIP:
{
    " "
|   "\t"
|   "\r"
}

// NOP LD PUSH POP IN OUT JMP JPC CALL RET CMP CAL EXIT
TOKEN :
{
	<I_CAL:	    "CAL">
	| <I_CALL:	"CALL">
	| <I_CMP:	"CMP">
	| <I_EXIT:	"EXIT">
	| <I_IN:	"IN">
	| <I_JMP:	"JMP">
	| <I_JPC:	"JPC">
	| <I_LD:	"LD">
	| <I_NOP:	"NOP">
	| <I_OUT:	"OUT">
	| <I_POP:	"POP">
	| <I_PUSH:	"PUSH">
	| <I_RET:	"RET">
//	| <I_ONE_OP: <I_CALL> | <I_PUSH> | <I_POP> | <I_JMP>>
//	| <I_TOW_OP: <I_IN> | <I_OUT>>
}

// T prefix for Token
TOKEN :
{
    <T_REGISTER : "R0" | "R1" | "R2" | "R3" | "RP" | "RF" |"RS" | "RB">
    | <DATA_TYPE: "DWORD" | "WORD" | "BYTE" | "FLOAT" | "INT">
    | <CAL_OP: "ADD" | "SUB" | "MUL" | "DIV" | "MOD">
    | <CMP_OP: "Z" | "B" | "BE" | "A" | "AE" | "NZ">
    | <T_LABEL: <IDENTIFIER> <COLON>>
}


List<Instruction> start():{
    List<Instruction> instructions = new ArrayList<Instruction>();
    Instruction inst;
    Token token;
}
{
     (
        "\n"
        |LOOKAHEAD(2) (
            token = <IDENTIFIER> <COLON>{addLabel(token);}
            | token = <T_LABEL>{addLabel(token);}
            | inst = instruction(){instructions.add(inst);}
        ) "\n"
     )*
     <EOF>
     {
        return instructions;
     }
}

Instruction instruction():{
    Instruction inst = new Instruction();
    Token o = null,dataType = null,calType = null,cmpType = null;
    Operand a = null,b = null;
}
{
    (
        (o = <I_EXIT> | o = <I_RET> | o = <I_NOP>)
        | (o = <I_CALL> |o = <I_PUSH> |o = <I_POP> |o = <I_JMP>) a = operand()
        | (o = <I_IN> | o = <I_OUT>) a = operand() <COMMA> b = operand()
        | o = <I_CAL> dataType = <DATA_TYPE> calType = <CAL_OP> a = operand() <COMMA> b = operand()
        | o = <I_LD> dataType = <DATA_TYPE> a = operand() <COMMA> b = operand()
        | o = <I_JPC> cmpType = <CMP_OP> a = operand()
    )
    {
        inst.setOpcode(Opcode.valueOf(o.image.toUpperCase())).setA(a).setB(b);
        if(dataType != null) inst.setDataType(DataType.valueOf(dataType.image.toUpperCase()));
        if(cmpType != null) inst.setCompareType(CompareType.valueOf(cmpType.image.toUpperCase()));
        if(calType != null) inst.setCalculateType(CalculateType.valueOf(calType.image.toUpperCase()));

        return inst;
    }
}

//Token oneOp():{
//    Token o = null;
//}{
//    (o = <I_CALL> |o = <I_PUSH> |o = <I_POP> |o = <I_JPC>){return o;}
//}

Operand operand():{
    Operand op = new Operand();
    Token token;
}
{

    (
        token = <T_REGISTER> {op.setValue(RegisterType.valueOf(token.image.toUpperCase())).setAddressingMode(REGISTER);}
        | token = <INTEGER_LITERAL> {op.setAddressingMode(IMMEDIATE).setValue(Integer.parseInt(token.image));}
//        | "["
//                token = <T_REGISTER>{op.setValue(RegisterType.valueOf(token.image.toUpperCase())).setAddressingMode(REGISTER_DEFERRED);}
//                | token = <INTEGER_LITERAL>{op.setAddressingMode(DIRECT).setValue(Integer.parseInt(token.image));}
//            "]"
        | token = <IDENTIFIER>{op.setAddressingMode(IMMEDIATE).setValue(-1);addLabelOperand(token,op);}
        |LOOKAHEAD(2) <LBRACKET> token = <INTEGER_LITERAL> <RBRACKET> {op.setAddressingMode(DIRECT).setValue(Integer.parseInt(token.image));}
        |LOOKAHEAD(2) <LBRACKET> token = <IDENTIFIER> <RBRACKET> {op.setAddressingMode(DIRECT).setValue(-1);addLabelOperand(token,op);}
        |LOOKAHEAD(2) <LBRACKET> token = <T_REGISTER> <RBRACKET> {op.setValue(RegisterType.valueOf(token.image.toUpperCase())).setAddressingMode(REGISTER_DEFERRED);}
    )
    {
        return op;
    }
}

// =========================================
// From java-1.7.jj
// =========================================

TOKEN :
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL>
      | <HEX_LITERAL>
      | <BINARY_LITERAL>
      | <OCTAL_LITERAL>
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] ((["_"])*["0"-"9"])* >      // Java 7 allows underscores
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])((["_"])*["0"-"9","a"-"f","A"-"F"])* >      // Java 7 allows underscores
|
  < #BINARY_LITERAL: "0" ["b","B"] (["0"-"1"])((["_"])*["0"-"1"])* >   // new for Java 7, also allows underscores
|
  < #OCTAL_LITERAL: "0" ((["_"])*(["0"-"7"]))* >      // Java 7 allows underscores
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])((["_"])*(["0"-"9"]))* "." ((["0"-"9"])((["_"])*(["0"-"9"]))*)* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])((["_"])*(["0"-"9"]))* (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])((["_"])*(["0"-"9"]))* <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])((["_"])*(["0"-"9"]))* (<EXPONENT>)? ["f","F","d","D"]
  >

|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'"
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}

/* IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}

/* SEPARATORS */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < AT: "@" >
}


/* OPERATORS */

TOKEN :
{
  < ASSIGN: "=" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < XOR: "^" >
| < REM: "%" >
| < LSHIFT: "<<" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
| < LSHIFTASSIGN: "<<=" >
| < RSIGNEDSHIFTASSIGN: ">>=" >
| < RUNSIGNEDSHIFTASSIGN: ">>>=" >
| < ELLIPSIS: "..." >
| < POINTER: "->" >
| < DIAMOND: "<>" >

}
